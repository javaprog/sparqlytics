// SPARQLytics: Multidimensional Analytics for RDF Data.
// Copyright (C) 2015  Michael Rudolf
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 2 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

options {
    JAVA_UNICODE_ESCAPE = true;
    STATIC = false;
}

PARSER_BEGIN(SPARQLyticsParser)
package de.tud.inf.db.sparqlytics.parser;

import org.apache.jena.query.Query;
import org.apache.jena.sparql.core.Var;
import org.apache.jena.sparql.expr.Expr;
import org.apache.jena.sparql.lang.sparql_11.SPARQLParser11;
import org.apache.jena.sparql.lang.sparql_11.SPARQLParser11TokenManager;
import org.apache.jena.sparql.syntax.Element;
import de.tud.inf.db.sparqlytics.model.*;
import de.tud.inf.db.sparqlytics.olap.*;
import de.tud.inf.db.sparqlytics.repository.DefaultRepository;
import de.tud.inf.db.sparqlytics.repository.MutableRepository;
import de.tud.inf.db.sparqlytics.repository.Repository;
import java.lang.reflect.Field;
import java.util.ArrayList;
import java.util.List;
import javax.naming.InitialContext;
import javax.naming.NamingException;

/**
 * Parses SPARQLytics commands. 
 *
 * @author Michael Rudolf
 */
public class SPARQLyticsParser {
    /**
     * Used for executing SPARQLytics commands.
     */
    private final Session session = new Session();

    /**
     * Maintains dimensions, measures and cubes.
     */
    private Repository repository = new DefaultRepository();

    /**
     * Whether the parser is in interactive mode.
     */
    private boolean interactive;

    /**
     * Returns the session used for executing SPARQLytics commands.
     */
    public Session getSession() {
        return session;
    }

    /**
     * Returns the repository maintaining dimensions, measures and cubes.
     */
    public Repository getRepository() {
        return repository;
    }

    /**
     * Returns whether the parser is in interactive mode.
     *
     * @see #setInteractive
     */
    public boolean isInteractive() {
        return interactive;
    }

    /**
     * Sets whether the parser is in interactive mode.
     *
     * @see #isInteractive
     */
    public void setInteractive(final boolean interactive) {
        this.interactive = interactive;
    }

    /**
     * Delegates parsing to a nested SPARQL parser, but one that will
     * use the JavaCharStream that the current parser is using.
     *
     * @param rule the SPARQL grammar rule to invoke
     * @return the result of the SPARQL grammar rule invocation
     * @throws ParseException if parsing fails on the given input
     */
    protected <T> T delegate(final SPARQLRule<T> rule) throws ParseException {
        // create a SPARQL parser that we will delegate to
        JavaCharStreamAdapter adapter = new JavaCharStreamAdapter(jj_input_stream);
        SPARQLParser11TokenManager tm = new SPARQLParser11TokenManager(adapter);
        SPARQLParser11 parser = new SPARQLParser11(tm);
        try {
            Field allowAggregatesInExpressions =
                    parser.getClass().getDeclaredField("allowAggregatesInExpressions");
            allowAggregatesInExpressions.setAccessible(true);
            allowAggregatesInExpressions.setBoolean(parser, true);
        } catch (NoSuchFieldException ex) {
            throw new AssertionError(ex);
        } catch (IllegalAccessException ex) {
            throw new AssertionError(ex);
        }

        // the parser fills in an empty query template
        parser.setQuery(session.getQuery());

        T result;
        try {
            // parse to the end of the query
            result = rule.execute(parser);

            // maybe push back the last tokens, which the sparql parser will
            // have consumed to know that the query was finished
            org.apache.jena.sparql.lang.sparql_11.Token token = parser.token;
            while ((token = token.next) != null) {
                jj_input_stream.backup(token.image == null ?
                    token.endColumn - token.beginColumn : token.image.length());
            }
        } catch (org.apache.jena.sparql.lang.sparql_11.ParseException ex) {
            throw (ParseException) new ParseException(
                    ex.getLocalizedMessage()).initCause(ex);
        } catch (org.apache.jena.sparql.lang.sparql_11.TokenMgrError ex) {
            throw (TokenMgrError) new TokenMgrError(ex.getLocalizedMessage(),
                    TokenMgrError.LEXICAL_ERROR).initCause(ex);
        }

        return result;
    }

    /**
     * Interface for code that invokes SPARQL grammar rules.
     *
     * @param <T> the type of the invoked rule's return value
     */
    protected static interface SPARQLRule<T> {
        /**
         * Executes the SPARQL grammar rule on the given SPARQL parser.
         *
         * @param parser the SPARQL parser to invoke the grammar rule on
         * @return the result of the SPARQL grammar rule invocation
         * @throws com.hp.hpl.jena.sparql.lang.sparql_11.ParseException
         *         if parsing fails on the given input
         */
        public T execute(SPARQLParser11 parser) 
            throws org.apache.jena.sparql.lang.sparql_11.ParseException;
    }

    /**
     * Invokes the {@code <ConditionalOrExpression>} SPARQL grammar rule.
     */
    private static class ConditionalOrExpressionRule implements SPARQLRule<Expr> {
        @Override
        public Expr execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.ConditionalOrExpression();
        }
    }

    /**
     * Invokes the {@code <DatasetClause>} SPARQL grammar rule.
     */
    private static class DatasetClauseRule implements SPARQLRule<Void> {
        @Override
        public Void execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            parser.DatasetClause();
            return null;
        }
    }

    /**
     * Invokes the {@code <GroupGraphPattern>} SPARQL grammar rule.
     */
    private static class GroupGraphPatternRule implements SPARQLRule<Element> {
        @Override
        public Element execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.GroupGraphPattern();
        }
    }

    /**
     * Invokes the {@code <Integer>} SPARQL grammar rule.
     */
    private static class IntegerRule implements SPARQLRule<Long> {
        @Override
        public Long execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.Integer();
        }
    }

    /**
     * Invokes the {@code <IRIREF>} SPARQL grammar rule.
     */
    private static class IRIREFRule implements SPARQLRule<String> {
        @Override
        public String execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.IRIREF();
        }
    }

    /**
     * Invokes the {@code <NumericExpression>} SPARQL grammar rule.
     */
    private static class NumericExpressionRule implements SPARQLRule<Expr> {
        @Override
        public Expr execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.NumericExpression();
        }
    }

    /**
     * Invokes the {@code <PrimaryExpression>} SPARQL grammar rule.
     */
    private static class PrimaryExpressionRule implements SPARQLRule<Expr> {
        @Override
        public Expr execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.PrimaryExpression();
        }
    }

    /**
     * Invokes the {@code <Prologue>} SPARQL grammar rule.
     */
    private static class PrologueRule implements SPARQLRule<Void> {
        @Override
        public Void execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            parser.Prologue();
            return null;
        }
    }

    /**
     * Invokes the {@code <String>} SPARQL grammar rule.
     */
    private static class StringRule implements SPARQLRule<String> {
        @Override
        public String execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.String();
        }
    }

    /**
     * Invokes the {@code <TriplesBlock>} SPARQL grammar rule.
     */
    private static class TriplesBlockRule implements SPARQLRule<Element> {
        @Override
        public Element execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.TriplesBlock(null);
        }
    }

    /**
     * Invokes the {@code <Var>} SPARQL grammar rule.
     */
    private static class VarRule implements SPARQLRule<Var> {
        @Override
        public Var execute(final SPARQLParser11 parser)
                throws org.apache.jena.sparql.lang.sparql_11.ParseException {
            return parser.Var();
        }
    }
}

PARSER_END(SPARQLyticsParser)

/* Token definitions */
SKIP :
{
     " "
  |  "\r"
  |  "\t"
  |  "\n"
}

TOKEN :
{
    < USING: "USING" >
  | < REPOSITORY: "REPOSITORY">
  | < BASE: "BASE">
  | < PREFIX: "PREFIX">
  | < SELECT: "SELECT" >
  | < FACTS: "FACTS" >
  | < DEFINE: "DEFINE" >
  | < DIMENSION: "DIMENSION" >
  | < FROM: "FROM" >
  | < WITH: "WITH" >
  | < LEVEL: "LEVEL" >
  | < AS: "AS" >
  | < MEASURE: "MEASURE" >
  | < WHERE: "WHERE" >
  | < CREATE: "CREATE" >
  | < CUBE: "CUBE" >
  | < OVER: "OVER" >
  | < DELETE: "DELETE" >
  | < SLICE: "SLICE" >
  | < DICE: "DICE" >
  | < RESET: "RESET" >
  | < FILTER: "FILTER" >
  | < ROLLUP: "ROLLUP" >
  | < DRILLDOWN: "DRILLDOWN" >
  | < COMPUTE: "COMPUTE" >
  | < STAR: "*" >
  | < ORDER: "ORDER" >
  | < BY: "BY" >
  | < ASC: "ASC" >
  | < DESC: "DESC" >
  | < LIMIT: "LIMIT" >
  | < OFFSET: "OFFSET" >
  | < EXIT: "EXIT" >
  | < LPAREN: "(" >
  | < RPAREN: ")" >
  | < COMMA: "," >
  | < SEMI: ";" >
}

SPECIAL_TOKEN :
{
   < SINGLE_LINE_COMMENT: "#" (~["\n", "\r"])* ("\n" | "\r" | "\r\n")? >
}

/* Parser grammar rules */
void Start() throws NamingException : {}
{
  (RepositorySelection())?
  Prologue()
  (DDL() | CubeDefinition() | CubeSelection() | OLAPOperation())*
}

void RepositorySelection() throws NamingException :
{
  String name;
}
{
  try {
    <USING> <REPOSITORY> name = String() <SEMI>
    { repository = (Repository) new InitialContext().lookup(name); }
  } catch (NamingException ex) {
    if (!interactive) {
      throw ex;
    }
    System.err.println(ex.getMessage());
  }
}

void DDL() : {}
{
    (<DEFINE> (DefineDimension() | DefineMeasure()))
  | (<DELETE> (DropDimension() | DropMeasure() | DropCube()))
}

void CubeDefinition() :
{
  CubeBuilder cubeBuilder;
}
{
    cubeBuilder = FactSelection()
    (DDL())*
    CreateCube(cubeBuilder)
}

CubeBuilder FactSelection() :
{
  Element pattern;
}
{
  <SELECT> <FACTS> pattern = GroupGraphPattern() <SEMI>
  {
    return new CubeBuilder(pattern);
  }
}

void DefineDimension() :
{
  String name;
  Element seedPattern;
  List<Level> levels = new ArrayList<Level>();
  Level level;
}
{
  <DIMENSION> name = String()
  <FROM> <LPAREN> seedPattern = TriplesBlock() <RPAREN>
  <WITH> <LPAREN> level = Level()
  { levels.add(level); }
  ( <COMMA> level = Level() { levels.add(level); } )*
  <RPAREN> <SEMI>
  {
    if (repository instanceof MutableRepository) {
      ((MutableRepository)repository).addDimension(
          new Dimension(name, seedPattern, levels));
    } else {
      UnsupportedOperationException ex = new UnsupportedOperationException(
          "The selected repository does not support modifications.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
}

Level Level() :
{
  String name;
  Expr expression;
}
{
  <LEVEL> name = String() <AS> expression = NumericExpression()
  { return new Level(name, expression); }
}

void DefineMeasure() :
{
  String name;
  Expr expression;
  Element seedPattern;
  String aggregationFunction;
}
{
  <MEASURE> name = String() <AS> expression = NumericExpression()
  <WHERE> <LPAREN> seedPattern = TriplesBlock() <RPAREN>
  <WITH> aggregationFunction = String() <SEMI>
  {
    if (repository instanceof MutableRepository) {
      ((MutableRepository)repository).addMeasure(
          new Measure(name, seedPattern, expression, aggregationFunction));
    } else {
      UnsupportedOperationException ex = new UnsupportedOperationException(
          "The selected repository does not support modifications.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
}

void CreateCube(CubeBuilder cubeBuilder) :
{
  String name;
  String temp;
}
{
  {
    if (cubeBuilder == null) {
      IllegalStateException ex = new IllegalStateException(
          "No facts have been selected.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
  <CREATE> <CUBE> name = String() <FROM>
  temp = String()
  { cubeBuilder.addDimension(repository.findDimension(temp)); }
  ( <COMMA> temp = String()
    { cubeBuilder.addDimension(repository.findDimension(temp)); }
  )*
  <WITH>
  temp = String()
  { cubeBuilder.addMeasure(repository.findMeasure(temp)); }
  ( <COMMA> temp = String()
    { cubeBuilder.addMeasure(repository.findMeasure(temp)); }
  )*
  <SEMI>
  {
    if (repository instanceof MutableRepository) {
      ((MutableRepository)repository).addCube(cubeBuilder.build(name));
    } else {
      UnsupportedOperationException ex = new UnsupportedOperationException(
          "The selected repository does not support modifications.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
}

void DropDimension() :
{
  String name;
}
{
  <DIMENSION> name = String() <SEMI>
  {
    if (repository instanceof MutableRepository) {
      ((MutableRepository)repository).removeDimension(repository.findDimension(name));
    } else {
      UnsupportedOperationException ex = new UnsupportedOperationException(
          "The selected repository does not support modifications.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
}

void DropMeasure() :
{
  String name;
}
{
  <MEASURE> name = String() <SEMI>
  {
    if (repository instanceof MutableRepository) {
      ((MutableRepository)repository).removeMeasure(repository.findMeasure(name));
    } else {
      UnsupportedOperationException ex = new UnsupportedOperationException(
          "The selected repository does not support modifications.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
}

void DropCube() :
{
  String name;
}
{
  <CUBE> name = String() <SEMI>
  {
    if (repository instanceof MutableRepository) {
      ((MutableRepository)repository).removeCube(repository.findCube(name));
    } else {
      UnsupportedOperationException ex = new UnsupportedOperationException(
          "The selected repository does not support modifications.");
      if (!interactive) {
        throw ex;
      }
      System.err.println(ex.getMessage());
    }
  }
}

void CubeSelection() :
{
  String name;
  String url;
}
{
  <USING> <CUBE> name = String() <OVER> url = IRIREF()
  DatasetClause()
  <SEMI>
  {
    session.setCube(repository.findCube(name));
    session.setSPARQLEndpointURL(url);
  }
}

boolean OLAPOperation() : {}
{
  try {
      (Slice() | Dice() | ResetFilter() | RollUp() | DrillDown() | ComputeMeasures())
      { return true; }
    | <EXIT> <SEMI>
      { return false; }
  } catch (ParseException ex) {
    if (!interactive) {
      throw ex;
    }
    System.err.println(ex.getMessage());
    while (true) {
      switch (getNextToken().kind) {
        case SEMI: return true;
      }
    }
  }
}

void Slice() :
{
  String dimension;
  String level;
  Expr value;
}
{
  <SLICE> <LPAREN> dimension = String() <COMMA>
                   level = String() <COMMA>
                   value = PrimaryExpression() <RPAREN> <SEMI>
  {
    session.execute(new Slice(dimension, level, value));
  }
}

void Dice() :
{
  String dimension;
  String level;
  Var variable;
  Expr predicate;
}
{
  <DICE> <LPAREN> dimension = String() <COMMA>
                  level = String() <AS> variable = Var() <COMMA>
                  predicate = ConditionalOrExpression() <RPAREN> <SEMI>
  {
    session.execute(new Dice(dimension, level, new Filter(variable, predicate)));
  }
}

void ResetFilter() : 
{
  String dimension;
  String level;
}
{
  <RESET> <FILTER> dimension = String() <COMMA>
                   level = String() <LPAREN> <RPAREN> <SEMI>
  {
    session.execute(new ResetFilter(dimension, level));
  }
}

void RollUp() :
{
  String name;
  Long steps;
}
{
  <ROLLUP> <LPAREN> name = String() <COMMA> steps = Integer() <RPAREN> <SEMI>
  {
    session.execute(new RollUp(name, steps.intValue()));
  }
}

void DrillDown() :
{
  String name;
  Long steps;
}
{
  <DRILLDOWN> <LPAREN> name = String() <COMMA> steps = Integer() <RPAREN> <SEMI>
  {
    session.execute(new DrillDown(name, steps.intValue()));
  }
}

void ComputeMeasures() :
{
  List<Measure> measures = new ArrayList<Measure>();
  String name;
  List<Compute.SortCondition> sortConditions = new ArrayList<Compute.SortCondition>();
  Long limit = null;
  Long offset = null;
}
{
  <COMPUTE> <LPAREN>
  ( <STAR>
    { measures.addAll(session.getCube().getMeasures()); }
  | LOOKAHEAD("\"")
    ( {
      // Reset after explicit lookahead, so that the beginning of string is not
      // swallowed and a fresh token is requested afterwards
      jj_input_stream.backup(1); jj_ntk = -1; token.next = null;
    }
    name = String()
    { measures.add(session.getCube().findMeasure(name)); }
    (<COMMA> name = String()
      { measures.add(session.getCube().findMeasure(name)); }
    )* )
  )
  <RPAREN>
  ( <ORDER> <BY>
    SortCondition(sortConditions) (<COMMA> SortCondition(sortConditions))*
    ( <LIMIT> limit = Integer() ( <OFFSET> offset = Integer() )? )?
  )? <SEMI>
  {
    session.execute(new Compute(measures, sortConditions, limit, offset));
  }
}

void SortCondition(List<Compute.SortCondition> sortConditions) :
{
  String name;
  int direction;
}
{
  name = String()
  ( <ASC> { direction = Query.ORDER_ASCENDING; }
  | <DESC> { direction = Query.ORDER_DESCENDING; } )
  {
    sortConditions.add(new Compute.SortCondition(name, direction));
  }
}

/* SPARQL grammar rule delegates */
void DatasetClause() : {}
{
  [ <FROM>
    {
      // Reset after explicit lookahead, so that the beginning of string is not
      // swallowed and a fresh token is requested afterwards
      jj_input_stream.backup(token.image == null ?
                    token.endColumn - token.beginColumn : token.image.length());
      jj_ntk = -1; token.next = null;
      delegate(new DatasetClauseRule());
    }
  ]
}

void Prologue() : {}
{
  [ (<BASE> | <PREFIX>)
    {
      // Reset after explicit lookahead, so that the beginning of string is not
      // swallowed and a fresh token is requested afterwards
      jj_input_stream.backup(token.image == null ?
                    token.endColumn - token.beginColumn : token.image.length());
      jj_ntk = -1; token.next = null;
      delegate(new PrologueRule());
    }
  ]
}

Element GroupGraphPattern() : {}
{
  { return delegate(new GroupGraphPatternRule()); }
}

String String() : {}
{
  { return delegate(new StringRule()); }
}

Element TriplesBlock() : {}
{
  { return delegate(new TriplesBlockRule()); }
}

Expr NumericExpression() : {}
{
  { return delegate(new NumericExpressionRule()); }
}

String IRIREF() : {}
{
  { return delegate(new IRIREFRule()); }
}

Expr PrimaryExpression() : {}
{
  { return delegate(new PrimaryExpressionRule()); }
}

Var Var() : {}
{
  { return delegate(new VarRule()); }
}

Expr ConditionalOrExpression() : {}
{
  { return delegate(new ConditionalOrExpressionRule()); }
}

Long Integer() : {}
{
  { return delegate(new IntegerRule()); }
}
